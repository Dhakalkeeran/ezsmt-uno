dnl Process this file with autoconf to produce a configure script.

AC_INIT(config.h.in)

AC_CONFIG_HEADER(config.h)

dnl >------ options ------<
AC_ARG_WITH([cmodels],
        [AS_HELP_STRING([--with-cmodels : build with cmodels])])

dnl >------ checks for programs ------<
AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PATH_PROG(MKATOMS, mkatoms)
if test -z "$MKATOMS"; then
   AC_MSG_ERROR([Unable to find the mkatoms program. Please install mkatoms.])
fi
AC_PATH_PROG(LPARSE, lparse)
AC_PATH_PROG(SMODELS, smodels)
AC_PATH_PROG(GRINGO, gringo)
AC_PATH_PROG(CLASP, clasp)
AC_PATH_PROG(CRMODELS2, crmodels2)
AC_PATH_PROG(SICSTUS, sicstus)
AC_PATH_PROG(BPROLOG, bp)
dnl if test -z "$SICSTUS"; then
dnl    AC_MSG_ERROR([Unable to find SICStus Prolog. Please install SICStus Prolog.])
dnl fi

dnl >------ checks for libraries ------<
AC_SEARCH_LIBS([regcomp],[regex])

dnl >------ checks for header files ------<
AC_HEADER_STDC
AC_CHECK_HEADERS(limits.h unistd.h dlfcn.h sys/resource.h signal.h sys/stat.h)
AC_HEADER_SYS_WAIT

AC_CHECK_HEADER(aspparser/parser.h,,
		AC_MSG_ERROR([Unable to find the aspparser header files. Please install dlv_rsig.]))

dnl >------ checks for typedefs ------<

dnl >------ checks for structures ------<

dnl >------ checks for compiler characteristics ------<

dnl >------ checks for library functions ------<
AC_CACHE_CHECK([for mkstemp],
ezcsp_cv_func_mkstemp,
[AC_TRY_LINK([#include <stdlib.h>
#include <unistd.h>],
[mkstemp("foo");],
ezcsp_cv_func_mkstemp=yes, ezcsp_cv_func_mkstemp=no)])
if test "$ezcsp_cv_func_mkstemp" = yes; then
  AC_DEFINE(HAVE_MKSTEMP, 1, [Define if mkstemp is available.])
fi

AC_MSG_CHECKING(for mkstemps in libiberty)
save_LIBS="$LIBS"
LIBS="-liberty $LIBS"
AC_LINK_IFELSE([
 AC_LANG_PROGRAM([[int mkstemps (char *pattern, int suffix_len);]],
    [[mkstemps("XXXXXX",0);]]
 )],
 [AC_MSG_RESULT(yes)
  HAVE_MKSTEMPS=yes
  AC_DEFINE(HAVE_MKSTEMPS, 1, [Define if mkstemps is available in libiberty.])
 ],
 [AC_MSG_RESULT(no)
  HAVE_MKSTEMPS=no
  LIBS="$save_LIBS"
])
AC_CHECK_FUNCS(gethostid)

AC_CHECK_LIB(aspparser,parser_version,,
		AC_MSG_ERROR([Unable to find the aspparser library. Please install dlv_rsig.]))

dnl >------ checks for system services ------<

AC_CONFIG_SUBDIRS(CMODELS/cmodels)

if test "x$with_cmodels" = "xyes"; then
	AC_SUBST([CMODELS_DIR],["CMODELS/cmodels"])
	AC_SUBST([CMODELS_LIB],["-lsat"])
	AC_DEFINE([HAVE_CMODELS], [1], [Define if cmodels is enabled])
else
	AC_SUBST([CMODELS_DIR],[""])
	AC_SUBST([CMODELS_LIB],[""])
dnl	AC_DEFINE([HAVE_CMODELS], [0], [Define if cmodels is enabled])
fi


AC_OUTPUT(Makefile)

if test -z "$LPARSE" -a -z "$GRINGO"; then
   AC_MSG_WARN([Neither lparse nor gringo could be found. At least one must be installed for ezcsp to work properly.])
fi

if test -z "$SMODELS" -a -z "$CLASP"; then
   AC_MSG_WARN([Neither smodels nor clasp could be found. At least one must be installed for ezcsp to work properly.])
fi

if test -z "$CRMODELS2"; then
   AC_MSG_WARN([crmodels2 could not be found. crmodels2 must be installed to have ezcsp process CR-Prolog files.])
fi

if test -z "$SICSTUS" -a -z "$BPROLOG"; then
   AC_MSG_WARN([Neither sicstus nor bprolog could be found. At least one must be installed for ezcsp to work properly.])
fi

if test "x$with_cmodels" = "xyes" -a ! -d "CMODELS/cmodels"; then
   AC_MSG_WARN([cmodels not found!!! Before running "make", you must unpack the cmodels distribution in CMODELS/ and rename directory cmodels-XXX to cmodels.])
fi

dnl check for broken gringo binaries
if ! test -z "$GRINGO"; then
if ! $GRINGO ./gringo-test.lp >/dev/null 2>/dev/null; then
   AC_MSG_WARN([The version of gringo installed at $GRINGO is known to have bugs. Please try compiling gringo from sources. To test gringo again, run /path/to/gringo/gringo gringo-test.lp. If it does not crash, then the version of gringo is ok. gringo-test.lp can be found in the main ezcsp directory. To use a different version of gringo, you can also specify option --grounder when calling ezcsp.])
fi
fi
